#!/usr/bin/env zsh

if [[ "$ZDOTDIR" = "" ]]; then
    export ZDOTDIR=~
fi

typeset -ga chpwd_functions
typeset -ga preexec_functions
typeset -ga precmd_functions

source $HOME/etc/shell/setup_prompt 

if [[ "$(basename $ZDOTDIR)" = "$LOGNAME" ]];
then
    $HOME/etc/shell/setup_symlinks
    $HOME/etc/shell/setup_xmonad
fi

fpath=($ZDOTDIR/etc/shell/completion $fpath)

export SHELL=`which zsh`

export PYTHONSTARTUP="$HOME/etc/utils/history.py"

export REPORTTIME=10

# Now deleting a word won't erase a whole path.
autoload -U select-word-style
select-word-style bash

# Prefer emacs bindings! I've found boxes where they default to vi bindings.
# Apparently the default changes based on your $EDITOR, but even if my EDITOR
# is set to emacs the some boxes default me to vi bindings without this.
bindkey -e

source $ZDOTDIR/etc/shell/prefs

# Apply zsh specific utils
source $ZDOTDIR/etc/shell/zsh_util

if [[ $(uname -s) = "Linux" ]]; then
	local curdir=$PWD
    cd ~/etc
    redo/redo all > ~/.redolog 2>&1
    cd "$curdir"
fi

# I want to see my core dumps
ulimit -c unlimited
# "core.%e.%p.%u.%t"

echo "DATE=$(date) HOST=$(hostname) USER=$LOGNAME TERM=$TERM" >> ~/.shell_log



cdr()
{
    # Have to run with unbuffer to make sure that it stops as
    # soon as it gets the first result.
    cd $(unbuffer find . -name "*$1*" -type d | head -1)
}

# if [[ -a $HOME/etc/cron/$(hostname).cron ]]; then
#     crontab $HOME/etc/cron/$(hostname).cron
# fi

################################################################################
# History settings
################################################################################
# Timestamps for when commands occurred make diagnosing problems easier
# ... unfortunately this breaks emacs reading of shell history so disable
# for now.
#setopt EXTENDED_HISTORY

# number of lines kept in history
export HISTSIZE=100000

# number of lines saved in the history after logout
export SAVEHIST=1000000

# append command to history file once executed
setopt inc_append_history

###############################################################################
# misc zsh settings
###############################################################################

setopt autopushd             # Push things onto the directory stack
setopt autocd                # cd to a dir when just its name is given
#setopt chasedots             # Resolve symlinks before resolving parent dirs
setopt autolist              # Do a list on ambiguous completion
setopt automenu              # Do a menu after <Tab><Tab>
setopt autoparamslash        # Append a slash for directory completion
setopt completealiases       # Treat aliases as commands
setopt listpacked            # Use variable column widths
setopt globdots              # Assume leading . for hidden files
setopt COMPLETE_IN_WORD      # allow completion in the middle of a word
setopt extended_glob         # Lets you negate patterns, e.g. all non .c files: ^*.c
setopt histignorespace       # don't put shell commands starting with space into history

# system beep is irritating for tab completion
unsetopt beep

# Add mime support for opening files
autoload -U zsh-mime-setup
zsh-mime-setup


###############################################################################
# utilities
###############################################################################

# Give some convenient shortcuts for pushing and popping folder stack
alias -r b='pushd +1 > /dev/null'
alias -r f='pushd -0 > /dev/null'
alias -r recent='ls -l -r --sort=time'

# allow tab completion in the middle of a word
alias l.='ls -d .*'
alias -r up="cd .."
alias -r upup="cd ../.."
alias -r upupup="cd ../../.."
alias -r upupupup="cd ../../../.."
alias cl="clear;ls"

# Intuitively, searches current folder and subfolders
search () {
	find \( -type f -o -type d \) -iname \*$1\*
}

# bash function to decompress archives - http://www.shell-fu.org/lister.php?id=375
extract () {
    if [ -f $1 ] ; then
        case $1 in
            *.tar.bz2)   tar xvjf $1        ;;
            *.tar.gz)    tar xvzf $1     ;;
            *.bz2)       bunzip2 $1       ;;
            *.rar)       unrar x $1     ;;
            *.gz)        gunzip $1     ;;
            *.tar)       tar xvf $1        ;;
            *.tbz2)      tar xvjf $1      ;;
            *.tgz)       tar xvzf $1       ;;
            *.zip)       unzip $1     ;;
            *.Z)         uncompress $1  ;;
            *.7z)        7z x $1    ;;
            *)           echo "'$1' cannot be extracted via >extract<" ;;
        esac
    else
        echo "'$1' is not a valid file"
    fi
}

################################################################################
# Completion settings
################################################################################

autoload -Uz compinit &> /dev/null && compinit

zstyle ':completion:*' completer \
    _expand \
    _complete \
    _match \
    _approximate \
    _prefix

# Cache completion results
zstyle ':completion:*' use-cache on

# Make lower-case input case insensitive
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# Better process completion
zstyle ':completion:*:processes' command 'ps -U $USERNAME -u $USERNAME -o pid,s,nice,stime,args'
zstyle ':completion:*:processes-names' command 'ps -U $USERNAME -u $USERNAME -o args'

# Ignore VCS directories when completing
zstyle ':completion:*:(all-|)files' ignored-patterns \
    '(|*/)CVS' \
    '(|*/).git' \
    '(|*/).svn'
zstyle ':completion:*:cd:*' ignored-patterns \
    '(|*/)#CVS' \
    '(|*/)#.git' \
    '(|*/)#.svn'

# Fix errors
zstyle ':completion:*:approximate:*' max-errors 2 numeric

# Ignore internal functions
zstyle ':completion:*:functions' ignored-patterns \
    '_*'

# Remove trailing slashes from directories
zstyle ':completion:*' squeeze-slashes true

# Attempt to complete many parts at once
zstyle ':completion:*' matcher-list '' 'r:|[._-]=** r:|=**' 'l:|=* r:|=*'

# Substitute things when tab completing
zstyle ':completion:*:expand:*' substitute true

# Insert as much as possible
zstyle ':completion:*:match:*' glob true
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:match:*' insert-unambiguous true

# Ignore where we are when completing directory names
zstyle ':completion:*' ignore-parents parent pwd directory

# Completion list settings
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s

# Separate groups
zstyle ':completion:*' group-name ''

# Highlight current choice
zstyle ':completion:*' menu select

HIST_DIR=$HOME
HIST_BACKUP_DIR=$HIST_DIR/.zhistory.backup
mkdir -p $HIST_DIR
mkdir -p $HIST_BACKUP_DIR
HIST_BACKUP_FILE="$HIST_BACKUP_DIR/zhistory.$(date "+%m%d%Y")"
export HISTFILE=$HIST_DIR/.zhistory
if [ ! -f "$HIST_BACKUP_FILE" ]; then
    # Delete files after a year.
    find "$HIST_BACKUP_DIR" -mtime +365 -type f -exec rm -rf {} \;

    # Make a backup
    cp -p "$HISTFILE" "$HIST_BACKUP_FILE"
fi

synergy_conf="$ZDOTDIR/etc/synergy/$(hostname).conf"
if [ -f "$synergy_conf" ] && \
    which synergys &> /dev/null && \
    ! tsk synergys &> /dev/null && \
    [ -n "$DISPLAY" ]
then
	    synergys -f --config $synergy_conf > $ZDOTDIR/.synergy_log 2>&1 &; disown
fi

if which xwrits &> /dev/null && \
    ! tsk xwrits &> /dev/null && \
    [ -n "$DISPLAY" ]
then
    xwrits +quota=3 +breakclock +multiscreen +mouse typetime=18 finger after 15 clock multiply=5:1.4 > $ZDOTDIR/.xwrits_log 2>&1 &; disown
fi

if [ -d "$ZDOTDIR/rmt" ]; then
	$ZDOTDIR/etc/bin/sync-repos
fi

source $ZDOTDIR/etc/shell/sandbox-support


zsh_version=$(zsh --version | cut -d' ' -f 2)
zsh_major=$(echo $zsh_version | cut -d'.' -f 1)
zsh_minor=$(echo $zsh_version | cut -d'.' -f 2)
zsh_micro=$(echo $zsh_version | cut -d'.' -f 3)

if (( $zsh_major < 4 )) || (( $zsh_minor < 3 )); then
		function precmd {
		    # Old versions of zsh won't run precmd_functions for us,
		    # so we implement it manually.
		    for func in $precmd_functions; do
				    $func
		    done
		}
fi

if which tarsnap &> /dev/null; then
    (if [ -f "$HOME/.make-backup-list" ]; then
         for i in $(cat "$HOME/.make-backup-list"); do
             name=$(echo $i | cut -d'@' -f1)
             dir=$(echo $i | cut -d'@' -f2)
             echo "Making today's backup for: $name" >> "$HOME/.backup-log"
             if ! make-backup -n "$name" "$dir" >> "$HOME/.backup-log" 2>&1; then
                 echo >&2 "Error making backup!!! For: $name"
             fi
         done
     fi) &; disown
fi

watch-build() {
    while true; do
        redo-ifchange $1 && $1
        echo >&2 "Exit code: $?"
        inotifywait -q -q $(redo-sources)
        clear
    done
}

