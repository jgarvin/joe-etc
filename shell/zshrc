export SHELL=`which zsh`

export REPORTTIME=10

# Now deleting a word won't erase a whole path.
autoload -U select-word-style
select-word-style bash

# Prefer emacs bindings! I've found boxes where they default to vi bindings.
# Apparently the default changes based on your $EDITOR, but even if my EDITOR
# is set to emacs the london boxes default me to vi bindings without this.
bindkey -e

if [[ "$ZDOTDIR" = "" ]]; then
    export ZDOTDIR=~
fi

export HISTFILE=~/.zhistory

# Apply TL specific settings
source $ZDOTDIR/etc/shell/prefs

# Apply zsh specific utils
source $ZDOTDIR/etc/shell/zsh_util

if [[ $(uname -s) = "Linux" ]]; then
    cd ~/etc
    redo/redo all > ~/.redolog 2>&1
    cd - > /dev/null
fi

# Sync configuration with github every login.
if ! gitsync > ~/.gitsynclog 2>&1; then
    echo "Error syncing git. See ~/.gitsynclog."
fi &

echo "DATE=$(date) HOST=$(hostname) USER=$LOGNAME TERM=$TERM" >> ~/.shell_log

cdr()
{
    # Have to run with unbuffer to make sure that it stops as
    # soon as it gets the first result.
    cd $(unbuffer find . -name "*$1*" -type d | head -1)
}

# if [[ -a $HOME/etc/cron/$(hostname).cron ]]; then
#     crontab $HOME/etc/cron/$(hostname).cron
# fi

################################################################################
# History settings
################################################################################
# Timestamps for when commands occurred make diagnosing problems easier
setopt EXTENDED_HISTORY

# number of lines kept in history
export HISTSIZE=100000

# number of lines saved in the history after logout
export SAVEHIST=100000

# append command to history file once executed
setopt inc_append_history

###############################################################################
# misc zsh settings
###############################################################################

setopt autopushd             # Push things onto the directory stack
setopt autocd                # cd to a dir when just its name is given
setopt chasedots             # Resolve symlinks before resolving parent dirs
setopt autolist              # Do a list on ambiguous completion
setopt automenu              # Do a menu after <Tab><Tab>
setopt autoparamslash        # Append a slash for directory completion
setopt completealiases       # Treat aliases as commands
setopt listpacked            # Use variable column widths
setopt globdots              # Assume leading . for hidden files
setopt COMPLETE_IN_WORD      # allow completion in the middle of a word
setopt extended_glob         # Lets you negate patterns, e.g. all non .c files: ^*.c

# system beep is irritating for tab completion
unsetopt beep

# Add mime support for opening files
autoload -U zsh-mime-setup
zsh-mime-setup


###############################################################################
# utilities
###############################################################################

# Give some convenient shortcuts for pushing and popping folder stack
alias -r b='pushd +1 > /dev/null'
alias -r f='pushd -0 > /dev/null'
alias -r recent='ls -l -r --sort=time'

# allow tab completion in the middle of a word
alias l.='ls -d .*'
alias -r up="cd .."
alias -r upup="cd ../.."
alias -r upupup="cd ../../.."
alias -r upupupup="cd ../../../.."
alias cl="clear;ls"

prepend-timestamp()
{
	awk '{ print strftime("%Y-%m-%d %H:%M:%S"), $0; }'
}

# Intuitively, searches current folder and subfolders
search () {
	find \( -type f -o -type d \) -name \*$1\*
}

# bash function to decompress archives - http://www.shell-fu.org/lister.php?id=375
extract () {
    if [ -f $1 ] ; then
        case $1 in
            *.tar.bz2)   tar xvjf $1        ;;
            *.tar.gz)    tar xvzf $1     ;;
            *.bz2)       bunzip2 $1       ;;
            *.rar)       unrar x $1     ;;
            *.gz)        gunzip $1     ;;
            *.tar)       tar xvf $1        ;;
            *.tbz2)      tar xvjf $1      ;;
            *.tgz)       tar xvzf $1       ;;
            *.zip)       unzip $1     ;;
            *.Z)         uncompress $1  ;;
            *.7z)        7z x $1    ;;
            *)           echo "'$1' cannot be extracted via >extract<" ;;
        esac
    else
        echo "'$1' is not a valid file"
    fi
}

################################################################################
# Completion settings
################################################################################

autoload -Uz compinit && compinit

zstyle ':completion:*' completer \
    _expand \
    _complete \
    _match \
    _approximate \
    _prefix

# Cache completion results
zstyle ':completion:*' use-cache on

# Make lower-case input case insensitive
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# Better process completion
zstyle ':completion:*:processes' command 'ps -U $USERNAME -u $USERNAME -o pid,s,nice,stime,args'
zstyle ':completion:*:processes-names' command 'ps -U $USERNAME -u $USERNAME -o args'

# Ignore VCS directories when completing
zstyle ':completion:*:(all-|)files' ignored-patterns \
    '(|*/)CVS' \
    '(|*/).git' \
    '(|*/).svn'
zstyle ':completion:*:cd:*' ignored-patterns \
    '(|*/)#CVS' \
    '(|*/)#.git' \
    '(|*/)#.svn'

# Fix errors
zstyle ':completion:*:approximate:*' max-errors 2 numeric

# Ignore internal functions
zstyle ':completion:*:functions' ignored-patterns \
    '_*'

# Remove trailing slashes from directories
zstyle ':completion:*' squeeze-slashes true

# Attempt to complete many parts at once
zstyle ':completion:*' matcher-list '' 'r:|[._-]=** r:|=**' 'l:|=* r:|=*'

# Substitute things when tab completing
zstyle ':completion:*:expand:*' substitute true

# Insert as much as possible
zstyle ':completion:*:match:*' glob true
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:match:*' insert-unambiguous true

# Ignore where we are when completing directory names
zstyle ':completion:*' ignore-parents parent pwd directory

# Completion list settings
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s

# Separate groups
zstyle ':completion:*' group-name ''

# Highlight current choice
zstyle ':completion:*' menu select

################################################################################
# Prompt settings
# Source: http://aperiodic.net/phil/prompt/
################################################################################

function precmd {

    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))

    ###
    # Truncate the path if it's too long.

    PR_FILLBAR=""
    PR_PWDLEN=""

    local promptsize=${#${(%):---(%n@%m:%l)---()--}}
    local pwdsize=${#${(%):-%~}}

    if [[ "$promptsize + $pwdsize" -gt $TERMWIDTH ]]; then
	    ((PR_PWDLEN=$TERMWIDTH - $promptsize))
    else
	PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $pwdsize)))..${PR_HBAR}.)}"
    fi

    ###
    # Get APM info.

    if apm &> /dev/null; then
	PR_APM_RESULT=`apm`
    fi

}

setprompt () {
    ###
    # Need this so the prompt will work.

    setopt prompt_subst


    ###
    # See if we can use colors.

    autoload colors zsh/terminfo
    if [[ "$terminfo[colors]" -ge 8 ]]; then
	colors
    fi
    for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
	eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
	eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
	(( count = $count + 1 ))
    done
    PR_NO_COLOUR="%{$terminfo[sgr0]%}"


    ###
    # See if we can use extended characters to look nicer.

    typeset -A altchar
    set -A altchar ${(s..)terminfo[acsc]}
	PR_SET_CHARSET="%{$terminfo[enacs]%}"
    PR_SHIFT_IN="%{$terminfo[smacs]%}"
    PR_SHIFT_OUT="%{$terminfo[rmacs]%}"
    PR_HBAR=${altchar[q]:--}
    PR_ULCORNER=${altchar[l]:--}
    PR_LLCORNER=${altchar[m]:--}
    PR_LRCORNER=${altchar[j]:--}
    PR_URCORNER=${altchar[k]:--}


    ###
    # Decide if we need to set titlebar text.

    case $TERM in
	xterm*)
	    PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
	    ;;
	# screen)
	#     PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
	#     ;;
	*)
	    PR_TITLEBAR=''
	    ;;
    esac


    ###
    # # Decide whether to set a screen title
    # if [[ "$TERM" == "screen" ]]; then
	# PR_STITLE=$'%{\ekzsh\e\\%}'
    # else
	# PR_STITLE=''
    # fi


    ###
    # APM detection

    if which apm &> /dev/null; then
	PR_APM='$PR_RED${PR_APM_RESULT[(w)5,(w)6]/\% /%%}$PR_LIGHT_BLUE:'
    else
	PR_APM=''
    fi


    ###
    # Finally, the prompt.

    PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
$PR_CYAN$PR_SHIFT_IN$PR_ULCORNER$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
$PR_GREEN%(!.%SROOT%s.%n)$PR_GREEN@%m:%l\
$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_HBAR${(e)PR_FILLBAR}$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
$PR_MAGENTA%$PR_PWDLEN<...<%~%<<\
$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_URCORNER$PR_SHIFT_OUT\

$PR_CYAN$PR_SHIFT_IN$PR_LLCORNER$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
%(?..$PR_LIGHT_RED%?$PR_BLUE:)\
${(e)PR_APM}$PR_YELLOW%D{%H:%M}\
$PR_LIGHT_BLUE:%(!.$PR_RED.$PR_WHITE)%#$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_NO_COLOUR '

    RPROMPT=' $PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_BLUE$PR_HBAR$PR_SHIFT_OUT\
($PR_YELLOW%D{%a,%b%d}$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_LRCORNER$PR_SHIFT_OUT$PR_NO_COLOUR'

    PS2='$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_BLUE$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT(\
$PR_LIGHT_GREEN%_$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT$PR_NO_COLOUR '
}

# To help emacs tramp mode and global desktop,
# turn off the fancy stuff.
if [[ "$TERM" = "dumb" || "$TERM" = "" ]]
then
  unsetopt zle
  unsetopt prompt_cr
  unsetopt prompt_subst
  unfunction precmd
  unfunction setprompt
  PS1='$ '
  PS2='$ '
  PROMPT='$ '
  RPROMPT='$ '
else
	# Creating new shells in GNU screen is jacked up on Solaris until you do this.
	if which stty &> /dev/null
	then
		stty sane

		# This disable Ctrl+s sending the STOP signal. This allows forward search in
		# zsh to work, and it prevents me from accidentally freezing all of my screen
		# instances (sending STOP to one sends it to all of them).
		stty -ixon
	fi

	# And this
	if which resize &> /dev/null
	then
		resize >& /dev/null
	fi

 	if [[ -s /etc/motd ]]; then
 		cat /etc/motd
 	fi
	setprompt
fi
